apply plugin: 'maven-publish'
apply plugin: 'signing'


afterEvaluate { project ->
    publishing {
        publications {
            withType(MavenPublication) {
                pom {
                    name = POM_NAME
                    description = POM_DESCRIPTION
                    url = POM_URL
                    licenses {
                        license {
                            name = POM_LICENCE_NAME
                            url = POM_LICENCE_URL
                            distribution = POM_LICENCE_DIST
                        }
                    }
                    developers {
                        developer {
                            id = "davebaol"
                            name = "davebaol"
                        }
                    }
                    scm {
                        connection = POM_SCM_CONNECTION
                        developerConnection = POM_SCM_DEV_CONNECTION
                        url = POM_SCM_URL
                    }
                }
            }
            //Check if we are a gradle plugin, if we are skip mavenJava
            if (!plugins.hasPlugin('java-gradle-plugin')) {
                mavenJava(MavenPublication) {

                    from components.java
                    versionMapping {
                        usage('java-api') {
                            fromResolutionOf('runtimeClasspath')
                        }
                        usage('java-runtime') {
                            fromResolutionResult()
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                url = version.endsWith('SNAPSHOT') ? getSnapshotRepositoryUrl() : getReleaseRepositoryUrl()

                if (getRepositoryUsername() || getRepositoryPassword()) {
                    credentials {
                        username = getRepositoryUsername()
                        password = getRepositoryPassword()
                    }
                }
            }
        }
    }

    signing {
        useGpgCmd()
        sign publishing.publications
    }

    //Simply using "required" in signing block doesn't work because taskGraph isn't ready yet.
    gradle.taskGraph.whenReady {
        tasks.withType(Sign) {
            onlyIf { isReleaseBuild() }
        }
    }
}